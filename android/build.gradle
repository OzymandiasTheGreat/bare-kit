plugins {
  id 'com.android.library'
}

def modules
try {
  modules = "bare-dev paths cmake".execute()
} catch (e) {
  modules = "cmd /c bare-dev paths cmake".execute()
}

group 'to.holepunch'

android {
  namespace 'to.holepunch.bare.kit'
  compileSdk 34
  ndkVersion '27.0.12077973'

  defaultConfig {
    minSdk 31
    targetSdk 34

    externalNativeBuild {
      cmake {
        targets 'bare_kit'
        arguments "-DCMAKE_MODULE_PATH=" + modules.text, "-DANDROID_STL=none"
      }
    }
  }

  buildFeatures {
    prefabPublishing true
  }

  prefab {
    bare_kit {
      libraryName "libbare-kit"
    }
  }

  externalNativeBuild {
    cmake {
      version '3.25.0+'
      path '../CMakeLists.txt'
    }
  }
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])
}
